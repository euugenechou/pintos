#
# A set of useful macros that can help debug Pintos.
#
# Include with "source" cmd in gdb.
# Use "help user-defined" for help.
#
# Author: Godmar Back <gback@cs.vt.edu>, Feb 2006
#         Eugene Chou <euchou@ucsc.edu>, Feb 2024
#
# $Id: gdb-macros,v 1.1 2006-04-07 18:29:34 blp Exp $
#

# for internal use
define offsetof
    set $rc = (char*)&((struct $arg0 *)0)->$arg1 - (char*)0
end

define list_entry
    offsetof $arg1 $arg2
    set $rc = ((struct $arg1 *) ((uint8_t *) ($arg0) - $rc))
end

# dump a Pintos list
define dumplist
    set $list = $arg0
    set $e = $list->head.next
    set $i = 0
    while $e != &(($arg0).tail)
        list_entry $e $arg1 $arg2
        set $l = $rc
        printf "pintos-debug: dumplist #%d: %p ", $i++, $l
        output *$l
        set $e = $e->next
        printf "\n"
    end
end
document dumplist
    Dump the content of a Pintos list,
    invoke as dumplist name_of_list name_of_struct name_of_elem_in_list_struct
end

define stack
    # Hack for default count value.
    if $argc > 0
        set $count = $arg0
    else
        set $count = 8
    end

    # Hack for default offset value.
    if $argc > 1
        set $offset = $arg1
    else
        set $offset = 0
    end

    set $i = 0
    set $addr = $esp + $offset

    while $i != $count
    	# Show count, byte offset, then distance from base pointer.
        printf "["
        printf "%02d:%04d|", $i, ($i * sizeof(void *))
        if $addr == $esp
            printf " esp"
        else
            if $addr == $ebp
                printf " ebp"
            else
                printf "%+04d", ((int)$addr - (int)$ebp)
            end
        end
        printf "] "

	# Show address and value.
        printf "%p -> ", $addr
        output/x *(int *)$addr

        set $addr = $addr + sizeof(void *)
        set $i = $i + 1
        printf "\n"
    end
end
document stack
    usage: stack [count] [offset]

    shows [count] stack values starting from address $esp + [offset]
    row format: [count:byte offset] address -> value

    arguments:
        count 	number of elements to dump [default: 8]
        offset  starting offset from $esp [default: 0]
end

# print a thread's backtrace, given a pointer to the struct thread *
define btthread
   if $arg0 == ($esp - ((unsigned)$esp % 4096))
	bt
   else
       set $saveEIP = $eip
       set $saveESP = $esp
       set $saveEBP = $ebp

       set $esp = ((struct thread *)$arg0)->stack
       set $ebp = ((void**)$esp)[2]
       set $eip = ((void**)$esp)[4]

       bt

       set $eip = $saveEIP
       set $esp = $saveESP
       set $ebp = $saveEBP
   end
end
document btthread
    Show the backtrace of a thread,
    invoke as btthread pointer_to_struct_thread
end

# print backtraces associated with all threads in a list
define btthreadlist
    set $list = $arg0
    set $e = $list->head.next
    while $e != &(($arg0).tail)
        list_entry $e thread $arg1
        printf "pintos-debug: dumping backtrace of thread '%s' @%p\n", \
                ((struct thread*)$rc)->name, $rc
        btthread $rc
        set $e = $e->next
        printf "\n"
    end
end
document btthreadlist
    Given a list of threads, print each thread's backtrace
    invoke as btthreadlist name_of_list name_of_elem_in_list_struct
end

# print backtraces of all threads (based on 'all_list' all threads list)
define btthreadall
    btthreadlist all_list allelem
end
document btthreadall
    Print backtraces of all threads
end

# print a correct backtrace by adjusting $eip
# this works best right at intr0e_stub
define btpagefault
    set $saveeip = $eip
    set $eip = ((void**)$esp)[1]
    backtrace
    set $eip = $saveeip
end
document btpagefault
    Print a backtrace of the current thread after a pagefault
end

set $lastcs = -1

# invoked whenever the program stops
define hook-stop
    #
    # Snippet borrowed from JOS's gdbinit
    #
    # There doesn't seem to be a good way to detect if we're in 16- or
    # 32-bit mode, but we always run with CS == 8 in 32-bit mode.
    if $cs == 8 || $cs == 27
      if $lastcs != 8 && $lastcs != 27
        set architecture i386
      end
      x/i $pc
    else
      if $lastcs == -1 || $lastcs == 8 || $lastcs == 27
        set architecture i8086
      end
      # Translate the segment:offset into a physical address
      printf "[%4x:%4x] ", $cs, $eip
      x/i $cs*16+$eip
    end
    set $lastcs = $cs
    # stopped at stub #0E = #14 (page fault exception handler stub)
    if ($eip == intr0e_stub)
        set $savedeip = ((void**)$esp)[1]
        # if this was in user mode, the OS should handle it
        # either handle the page fault or terminate the process
        if ($savedeip < 0xC0000000)
            printf "pintos-debug: a page fault exception occurred in user mode\n"
            printf "pintos-debug: hit 'c' to continue, or 's' to step to intr_handler\n"
        else
            # if this was in kernel mode, a stack trace might be useful
            printf "pintos-debug: a page fault occurred in kernel mode\n"
            btpagefault
        end
    end
end

# load symbols for a Pintos user program
define loadusersymbols
    shell objdump -h $arg0 | awk '/.text/ { print "add-symbol-file $arg0 0x"$4 }' > .loadsymbols
    source .loadsymbols
    shell rm -f .loadsymbols
end
document loadusersymbols
    Load the symbols contained in a user program's executable.
    Example:
        loadusersymbols tests/userprog/exec-multiple
end

define debugpintos
    target remote localhost:1234
end
document debugpintos
    Attach debugger to pintos process
end
